/*
    Gradle Python extension
    Project: HSPYLIB
    Created: 1st July, 2020
*/

ext {
  pythonPath = "${sourceRoot}:${sourceRoot}/main:${sourceRoot}/test"
}

/* Helper Functions -------------------------------------------------------- */

Collection dirsByPattern(String baseDir, String pattern) {
  def paths = []
  fileTree(baseDir).visit { FileVisitDetails details ->
      if (details.isDirectory() && details.name ==~ pattern) paths << details.file.path
  }
  return paths
}

String dirName(File file) {
  file.getParentFile().getPath()
}

/***** Install Group */

/* Used to created the __init__.py files { */
def initTemplate = """# _*_ coding: utf-8 _*_
#
# HSPyLib v${projectVersion}
#
# Package: %package%

__all__ = %modules%
"""
/* } */

/* Find and synchronize all python packages */
task syncPythonPackages(type: Task) {
  group = 'Install'
  description = 'Find and synchronize all python packages'
  doLast {
    project.file("${sourceRoot}").traverse(type: groovy.io.FileType.DIRECTORIES) { directory ->
      directory.eachFileMatch(~/^__init__\.py$/) { initFile ->
        def pkg = initFile.getCanonicalPath()
          .replaceAll("${sourceRoot}/", "")
          .replaceAll("\\/", "\\.")
          .replaceAll("\\.?__init__\\.py", "")
        def modules = []
        initFile.getParentFile().eachFile() { module ->
          if (!module.name.startsWith("__init__") &&
              !module.name.startsWith("__pycache__") &&
              ((module.isFile() && module.name.endsWith(".py")) ||
              new File(module.getCanonicalPath() + "/__init__.py").exists())) {
            if (!module.isFile()) {
              println "Syncing package: ${initFile.getParentFile()}/${module.name}"
            }
            modules << "'${module.name.replaceAll("\\.py", "")}'"
          }
        }
        initFile.write initTemplate
          .replaceAll("%package%", pkg)
          .replaceAll("%modules%", modules.sort().toString())
          .replaceAll("\\[", "\\[\n  ")
          .replaceAll(", ", ", \n  ")
          .replaceAll("\\]", "\n]")
      }
    }
  }
}

/* Install this application as a python package using module's setup.py */
task installModule(type: Task) {
  group = 'Install'
  description = "Install the module as a python package using module's setup.py"
  dependsOn syncPythonPackages
  doLast {
    def depsFile = new File(projectDir, "requirements.txt")
    depsFile.write ''
    project.ext.deps.each { dep ->
      depsFile.append dep + '\n'
    }
    copy {
        from("$rootProject.projectDir") {
          include 'README.md'
          include 'VERSION'
          include 'requirements.txt'
        }
        into "${sourceRoot}/main"
    }
    fileTree("${sourceRoot}").matching {
      include "**/setup.py"
    }.each { File module ->
        def moduleDir = dirName(module)
        println "Installing ${rootProject.name} from ${moduleDir} -> ${module.path}"
        exec {
          workingDir "${moduleDir}"
          commandLine rootProject.python, '-m', 'pip', 'install', '--user', '.'
        }
      }
  }
}

/* Uninstall the module as a python package using module's setup.py */
task uninstallModule(type: Task) {
  group = 'Install'
  description = "Uninstall the module as a python package using module's setup.py"
  doLast {
    fileTree("${sourceRoot}/main").matching {
      include "**/setup.py"
    }.each { File file ->
        def moduleDir = "${file.path.replace(file.name, "")}"
        println "Removing python modules from: ${file.path}"
        exec {
          workingDir "${moduleDir}"
          commandLine rootProject.python, '-m', 'pip', 'uninstall', '-y', "${rootProject.name}"
        }
      }
  }
}

/***** Build Group */

/* Clean all compiled (*.py,*.qrc) and cached (__pycache__$) python files from "${sourceRoot}" */
task cleanPython(type: Delete) {
  group = 'Build'
  description = 'Clean all compiled (*.py,*.qrc) files and cached (__pycache__/) directories from sourceRoot'
  doLast {
    println 'Cleaning all compiled (*.py,*.qrc) files and cached (__pycache__/) directories  from sourceRoot...'
    delete fileTree(sourceRoot).matching {
      include "**/*.pyc"
      include "**/*.qrc"
    }
    delete dirsByPattern(sourceRoot, /.*__pycache__$/)
  }
}

/* Compile all python files from sourceRoot */
task compilePython(type: Task) {
  group = 'Build'
  description = 'Compile all python files from sourceRoot'
  doLast {
    fileTree("${sourceRoot}").matching {
      include "**/*.py"
      exclude "**/__init__.py"
    }.each { File file ->
        println "Compiling Python file -> ${file.name}"
        exec {
          commandLine rootProject.python, '-m', 'py_compile', file.path
        }
      }
  }
}

/* Compile all QT resources from sourceRoot */
task compileQrc(type: Task) {
  group = 'Build'
  description = 'Compile all Qt resource files from sourceRoot'
  doLast {
    fileTree("${sourceRoot}").matching {
      include "**/*.qrc"
    }.each { File file ->
        println "Compiling Qt Resource -> ${file.name}"
        exec {
          commandLine rootProject.pyrcc, '-m', 'py_compile', file.path
        }
      }
  }
}

task check(type: Task) {
  group = 'Verification'
  description = 'Run all Qt python tests "${sourceRoot}/test/**/test_*.py"'
  dependsOn compilePython
  doLast {
    fileTree("${sourceRoot}/test").matching {
      include "**/test_*.py"
    }.each { File file ->
        println ""
        println "Running unittests -> ${file.name}"
        println "  |- PYTHON_PATH: ${rootProject.ext.pythonPath}"
        println ""
        exec {
          workingDir = dirName(file)
          environment PYTHONPATH: "${rootProject.ext.pythonPath}"
          commandLine rootProject.python, '-m', 'unittest', '-b', '-f', '-v', file.path
        }
      }
  }
}

/***** Task Wrappers */

task clean(type: Task) {
  group = 'Build'
  dependsOn cleanPython
}

task compile(type: Task) {
  group = 'Build'
  dependsOn compilePython
  dependsOn compileQrc
}

task test(type: Task) {
  group = 'Verification'
  dependsOn compile
  mustRunAfter compile
  dependsOn check
}

task build(type: Task) {
  group = 'Build'
  dependsOn clean
  mustRunAfter clean
  dependsOn installDependencies
  mustRunAfter installDependencies
  dependsOn test
}

task buildOnly(type: Task) {
  group = 'Build'
  dependsOn clean
  mustRunAfter clean
  dependsOn installDependencies
  mustRunAfter installDependencies
  dependsOn compile
  mustRunAfter compile
}

task install(type: Task) {
  group = 'Install'
  dependsOn installDependencies
  mustRunAfter installDependencies
  dependsOn buildOnly
  mustRunAfter buildOnly
  dependsOn uninstallModule
  mustRunAfter uninstallModule
  dependsOn installModule
}
