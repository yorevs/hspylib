/*
    Gradle documentation generation extension

    Created: 30th April, 2021
    License: MIT - Please refer to <https://opensource.org/licenses/MIT>
    Copyright 2023, HsPyLib team
*/

import java.text.SimpleDateFormat

def year = new SimpleDateFormat("yyyy").format(new Date())
def curDate = new SimpleDateFormat("EEE, d MMM yyyy").format(new Date())
def apiSrcDir = "${sourceRoot}/main/${project.name}"

ext {
  apiDocsDir = "${rootProject.projectDir}/docs/api"
}

/* File Headers Section ---------------------------------------------------- */

/**** PYTHON ****/
def PY_FILE_HEADER = """#!/usr/bin/env python3
# -*- coding: utf-8 -*-

\"\"\"
   @project: ${project.ext.app_name}
   @package: %package%
      @file: %filename%
   @created: ${curDate}
    @author: ${project.ext.author}"
      @site: ${project.ext.siteUrl}
   @license: MIT - Please refer to <https://opensource.org/licenses/MIT>

   Copyright ${year}, HSPyLib team
\"\"\"
"""

/* Helper Functions -------------------------------------------------------- */

Collection filesByPattern(String baseDir, String pattern) {
  def paths = []
  fileTree(baseDir).visit { FileVisitDetails details ->
      if (!details.isDirectory() && details.name ==~ pattern) paths << details.file.path
  }
  return paths
}

String getPackageName(File file) {
  return file.getParentFile()
    .getCanonicalPath()
    .replaceAll("${sourceRoot}", "${project.ext.app_name.toLowerCase()}")
    .replaceAll("\\/", "\\.")
}

String processPythonHeader(File file, String header_template) {
    def pkg = getPackageName(file)
    def header = header_template
      .replaceAll("%filename%", file.getName())
      .replaceAll("%package%", pkg)

    return header
}


/* Tasks ------------------------------------------------------------------- */

/* Generate API documentation based on docstrings */
task autoDocApi(type: Exec) {
  group = 'Documentation'
  description = 'Generate API documentation based on docstrings'
  dependsOn syncPythonPackages
  commandLine rootProject.python, '-m', 'pdoc', '-o', "${apiDocsDir}/${project.name}", apiSrcDir
}

/* Optimize python imports */
task optimizeImports(type: Exec) {
  group = 'Documentation'
  description = 'Optimize python imports'
  commandLine rootProject.python, '-m', 'isort', '--fas', '--ds', sourceRoot
}

/* Reformat the python code using black */
task reformatCode(type: Exec) {
  group = 'Documentation'
  description = 'Reformat the python code using black'
  finalizedBy optimizeImports
  commandLine rootProject.python, '-m', 'black', '--fast', '-C', '-t', 'py310', '-l', 120, sourceRoot
}

/* Synchronize python file headers (doc strings) */
task syncPythonHeaders(type: Task) {
  group = 'Documentation'
  description = 'Synchronize python file headers (doc strings)'
  finalizedBy optimizeImports
  doLast {
    def replaceHeaders = project.hasProperty("replaceHeaders")
    println "Synchronizing python headers replace: ${replaceHeaders}"
    def files = filesByPattern(sourceRoot, /.*(?<!__init__)\.py$/)
    files.each {
      println "Processing file: ${it}"
      def fh_file = new File(it)
      def headers = processPythonHeader(fh_file, PY_FILE_HEADER)
      def parseState = 'headers' // headers -> imports -> code
      def imports = []
      def code = []
      def old_headers = []
      def has_old_headers = false
      fh_file.eachLine { String line ->
        if ('headers'.equals(parseState) && !(line ==~ /^(import|from) .*/)) {
          // Ignore all header lines (we could keep them tho)
          old_headers << line
          if (!replaceHeaders && line.startsWith('"""')) {
            has_old_headers = true
          }
        }
        else if ('imports'.equals(parseState) || line ==~ /^(import|from .* import) .*/) {
          imports << line
          if (line.trim().endsWith('\\') || line.trim().endsWith(',') || line.trim().endsWith('(')) {
            parseState = 'imports'
          }
          else{
            parseState = parseState.equals('headers') ? 'code' : parseState
          }
        } else if ('code'.equals(parseState) || !(line.isEmpty() && line ==~ /^(import|from .* import) .*/)) {
          code << line
        }
      }
      fh_file.setText("${has_old_headers ? old_headers.join('\n') : headers }\n${imports.join('\n')}${code.join('\n')}\n")
    }
    println "Finished processing all python files"
  }
}
