/*
    Gradle PyPi publisher extension
    Project: HSPyLib
    Created: 23rd April, 2021
*/
import groovy.json.JsonSlurper

ext {
  pypiModuleUrl = "https://pypi.org/pypi/${app_name}/json"
  pypiRepoUrl = System.getenv("PYPI_REPOSITORY") ?: 'https://upload.pypi.org/legacy/'
  pypiUsername = System.getenv("PYPI_USERNAME") ?: System.getenv("USER")
  pypiPassword = System.getenv("PYPI_PASSWORD") ?: ''
}


/* Helper Functions -------------------------------------------------------- */

Collection dirsByPattern(String baseDir, String pattern) {
  def paths = []
  fileTree(baseDir).visit { FileVisitDetails details ->
      if (details.isDirectory() && details.name ==~ pattern) paths << details.file.path
  }
  return paths
}

String dirName(File file) {
  file.getParentFile().getPath()
}

/* Tasks ------------------------------------------------------------------- */

/* Copy LICENSE file into main folder */
task copyLicense(type: Copy) {
  group = 'Publish'
  description = "Copy LICENSE file into main folder"
  from(rootDir) {
    include 'LICENSE.md'
  }
  into "$sourceRoot/main"
}

/* Generate PyPi distribution files */
task dist(type: Task) {
  group = 'Publish'
  description = 'Generate PyPi distribution files'
  dependsOn cleanDist
  dependsOn copyLicense
  dependsOn patchVersion
  dependsOn requirements
  doLast {
    fileTree("$sourceRoot").matching {
        include "**/setup.py"
      }.each { File module ->
          def moduleDir = dirName(module)
          println "Generating distribution files -> $moduleDir"
          exec {
            workingDir "$moduleDir"
            commandLine rootProject.python, "setup.py", 'sdist', 'bdist_wheel'
          }
        }
    }
}

/* Check files created in dist folder */
task checkDist(type: Task) {
  group = 'Publish'
  description = 'Check files created in dist folder'
  dependsOn dist
  doLast {
    fileTree(sourceRoot).matching {
        include "**/setup.py"
      }.each { File module ->
        def moduleDir = dirName(module)
        def distDir = "${moduleDir}/dist"
        println "Checking distribution files -> $distDir"
        exec {
          workingDir moduleDir
          commandLine rootProject.python, '-m', 'twine', 'check', "${distDir}/*"
        }
    }
  }
}

/* Publish the module to PyPi repository */
task publish(type: Task) {
  group = 'Publish'
  description = "Publish the module to PyPi repository"
  dependsOn checkDist
  dependsOn buildOnly
  def url = project.ext.pypiRepoUrl
  def un = project.ext.pypiUsername
  def pw = project.ext.pypiPassword
  doLast {
    fileTree("$sourceRoot").matching {
        include "**/setup.py"
      }.each { File module ->
        def moduleDir = dirName(module)
        def distDir = "${moduleDir}/dist"
        println '--'
        println "|-version: ${project.ext.app_version}"
        println "|-setup: ${module}"
        println "|-url: ${url}"
        println "|-user: ${un}"
        println "|-password: __token__"
        println "|-dist: ${distDir}/*"
        println '--'
        println "Publishing module to PyPi => ${url}"
        exec {
          workingDir moduleDir
          commandLine rootProject.python, '-m', 'twine', 'upload',
            '--verbose',
            '--repository-url', "${url}",
            '-u', "${un}",
            '-p', "${pw}",
            "${distDir}/*"
        }
    }
  }
}

/* Show PyPi module details */
task pypiShow(type: Task) {
  group = 'Publish'
  description = "Show PyPi module details"
  doLast {
    def tempDir = System.getenv("TEMP") ?: '/tmp'
    def url = project.ext.pypiModuleUrl
    def outFile = "${tempDir}/${app_name}-info.json"
    exec {
      commandLine 'curl', '-s', '-o', outFile, '-O', url
    }
    def json = new JsonSlurper().parseText(new File(outFile).getText())
    println("\n--------------------------------------------------------------------------------")
    println("|-Summary: ${json.info.summary}")
    println("|-Version: ${json.info.version}")
    println("|-License: ${json.info.license}")
    println("|-Python: ${json.info.requires_python}")
    println("|-Dependencies: ${json.info.requires_dist}")
    println("|-Keywords: ${json.info.keywords}")
    println("--------------------------------------------------------------------------------")
  }
}
