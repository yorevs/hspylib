/*
    Gradle PyPi publisher extension

    Created: 23rd April, 2021
    License: MIT - Please refer to <https://opensource.org/licenses/MIT>
    Copyright 2023, HsPyLib team
*/

import groovy.json.JsonSlurper

ext {
  pypiTestUrl = "https://test.pypi.org/legacy/${app_name}/json"
  pypiModuleUrl = "https://pypi.org/pypi/${app_name}/json"
  pypiRepoUrl = System.getenv("PYPI_REPOSITORY") ?: 'https://upload.pypi.org/legacy/'
  pypiUsername = System.getenv("PYPI_USERNAME") ?: System.getenv("USER")
  pypiPassword = System.getenv("PYPI_PASSWORD") ?: ''
}


/* Helper Functions -------------------------------------------------------- */

Collection dirsByPattern(String baseDir, String pattern) {
  def paths = []
  fileTree(baseDir).visit { FileVisitDetails details ->
      if (details.isDirectory() && details.name ==~ pattern) paths << details.file.path
  }
  return paths
}

String dirName(File file) {
  file.getParentFile().getPath()
}

/* Tasks ------------------------------------------------------------------- */

/* Copy LICENSE file into main folder */
task copyLicense(type: Copy) {
  group = 'Publish'
  description = "Copy LICENSE file into main folder"
  from(rootDir) {
    include 'LICENSE.md'
  }
  into "$sourceRoot/main"
}

/* Check files created in dist folder */
task checkDist(type: Task) {
  group = 'Publish'
  description = 'Check files created in dist folder'
  doLast {
    fileTree(sourceRoot).matching {
        include "**/setup.py"
      }.each { File module ->
        def moduleDir = dirName(module)
        def distDir = "${moduleDir}/dist"
        println "Checking distribution files -> $distDir"
        exec {
          workingDir moduleDir
          commandLine rootProject.python, '-m', 'twine', 'check', "${distDir}/*"
        }
    }
  }
}

/* Generate PyPi distribution files */
task sdist(type: Task) {
  group = 'Publish'
  description = 'Generate PyPi distribution files'
  dependsOn copyLicense
  dependsOn patchVersion
  dependsOn syncRequirements
  finalizedBy checkDist
  doLast {
    fileTree("$sourceRoot").matching {
        include "**/setup.py"
      }.each { File module ->
          def moduleDir = dirName(module)
          println "Generating distribution files -> $moduleDir"
          exec {
            workingDir "$moduleDir"
            commandLine rootProject.python, "-m", "build", "--sdist"
          }
          exec {
            workingDir "$moduleDir"
            commandLine rootProject.python, "-m", "build", "--wheel"
          }
        }
    }
}

/* Publish the module to PyPi repository */
task publish(type: Task) {
  group = 'Publish'
  description = "Publish the module to PyPi repository"
  dependsOn sdist
  finalizedBy cleanDist
  def url = project.ext.pypiRepoUrl
  def un = project.ext.pypiUsername
  def pw = project.ext.pypiPassword
  doLast {
    fileTree("$sourceRoot").matching {
        include "**/setup.py"
      }.each { File module ->
        def moduleDir = dirName(module)
        def distDir = "${moduleDir}/dist"
        println "--- ${project.name} details ---"
        println "|-version: ${project.ext.app_version}"
        println "|-setup: ${module}"
        println "|-url: ${url}"
        println "|-user: ${un}"
        println "|-password: __token__"
        println "|-dist: ${distDir}/*"
        println '-------------------------------'
        println "Publishing module to PyPi => ${url}"
        exec {
          workingDir moduleDir
          commandLine rootProject.python, '-m', 'twine', 'upload',
            '--verbose',
            '--repository-url', "${url}",
            '-u', "${un}",
            '-p', "${pw}",
            "${distDir}/*"
        }
    }
  }
}

/* Show PyPi module details */
task pypiShow(type: Task) {
  group = 'Publish'
  description = "Show PyPi module details"
  doLast {
    def tempDir = System.getenv("TEMP") ?: '/tmp'
    def url = project.ext.pypiModuleUrl
    def outFile = "${tempDir}/${app_name}-info.json"
    exec {
      commandLine 'curl', '-s', '-o', outFile, '-O', url
    }
    def json = new JsonSlurper().parseText(new File(outFile).getText())
    println("\n--------------------------------------------------------------------------------")
    println("|-Summary: ${json.info.summary}")
    println("|-Version: ${json.info.version}")
    println("|-License: ${json.info.license}")
    println("|-Python: ${json.info.requires_python}")
    println("|-Dependencies: ${json.info.requires_dist}")
    println("|-Keywords: ${json.info.keywords}")
    println("--------------------------------------------------------------------------------")
  }
}
