/*
    Gradle root build file
    Project: HSPyLib
    Created: 1st July, 2020
*/
plugins {
  id 'idea'
  id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

idea {
  module {
    settings {
      rootModuleType = 'PYTHON_MODULE'
    }
    excludeDirs += file('.idea')
    excludeDirs += file('.gradle')
    excludeDirs += file('.vscode')
    excludeDirs += file('run-configs')
    excludeDirs += file('devel')
  }
}

configurations {
    modules
}

dependencies {
    project(':cfman')
    project(':clitt')
    project(':datasource')
    project(':firebase')
    project(':hqt')
    project(':hspylib')
    project(':kafman')
    project(':setman')
    project(':vault')
}

subprojects {

  tasks.withType(Exec) {
    doFirst {
      println ">>> ${commandLine}"
    }
  }

  apply from: "$projectDir/gradle.properties"

  project.afterEvaluate {
    assert project.ext.has("sourceRoot")
    assert project.ext.has("pythonPath")
  }
}

/* Helper Functions -------------------------------------------------------- */
String detectSpace() {

  def output = new ByteArrayOutputStream()
  exec {
    commandLine 'sh', '-c', 'python3 -c "from sys import prefix, base_prefix; print(prefix != base_prefix);"'
    standardOutput = output
  }
  def is_venv = Boolean.parseBoolean(output.toString())

  return is_venv ? '--global' : '--user'
}

/* Project extension properties */
ext {
  python = "python" + (System.getenv('PYTHON_VERSION') ?: '3.11')
  pyrcc = "pyrcc" + (System.getenv('PYRCC_VERSION') ?: '5')
  buildTools = "${buildTools}".split(",")
  startTime = System.currentTimeMillis()
  verbose = findProperty('verbose') ?: false
  modulesDir = "${project.projectDir}/modules/"
  buildToolsFile = new File("${project.projectDir}/buildTools.txt")
  space = detectSpace()
}

apply from: "$rootDir/gradle/idea.gradle"

/* List project modules */
task listModules(type: Task) {
  group = 'Other'
  description = 'List project modules'
  doLast {
    println("\nListing " + project.name + " modules ...")
    println("Modules Dir: " + modulesDir + "\n")
    subprojects.forEach {
      println(sprintf(
        "Module: %-12s Version: %-10s Source: %s",
        it.name, it.app_version, it.sourceRoot.replaceAll(modulesDir, '')
      ))
    }
  }
}

/* Install hspylib build tools using Global space */
task installBuildTools(type: Task) {
  group = 'Install'
  description = 'Install hspylib build tools'
  doLast {
    buildToolsFile.setText("###### Build Tools Files for $project.name ######\n\n")
    println "Installing \"${project.name}\" build tools: "
    println "Packages:"
    rootProject.buildTools.each { tool ->
      println "  |-${tool.trim()}"
      buildToolsFile.append("${tool.trim()}\n")
    }
    println "Space: --global"
    def args = [
      rootProject.python, '-m', 'pip', 'install', '-q',
      project.ext.space, '--upgrade', '-r', buildToolsFile,
      '--no-warn-script-location', '--break-system-packages'
    ]
    exec {
      commandLine args.flatten()
    }
  }
}

/* Generate a changelog with the commits from last git tag */
task changelog(type: Task) {
  group = 'Publish'
  description = "Generate a changelog with the commits from last git tag"
  doLast {
    def out = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--tags', '--abbrev=0', 'HEAD^'
      standardOutput = out
    }
    def lastTag = out.toString().trim()
    if (!lastTag) {
      raise new StopActionException("Unable to fetch latest tag")
    }
    out.reset()
    exec {
      commandLine 'git', 'log', '--oneline', "--pretty='%h %ad %s'", '--date=short', "${lastTag}..HEAD"
      standardOutput = out
      errorOutput = standardOutput
    }
    println("\nShowing changelog commits from: ${lastTag}")
    println("--------------------------------------------------------------------------------")
    out.each(s ->
      println(s)
    )
  }
}
