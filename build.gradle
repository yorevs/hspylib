
buildscript {
  project.ext.gradleProperties = "$rootProject.projectDir/properties.gradle"
  apply from: gradleProperties
}

apply from: "properties.gradle"

ext {
  startTime = System.currentTimeMillis()
  python = "python${pythonVersion}"
  pyrcc = "pyrcc3${pyrccVersion}"
}

def sourceRoot = "${rootDir}/src"

def dependencies = [
 'PyQt5', 'cryptography', 'pycodestyle', 'pymysql'
]

wrapper {
  gradleVersion="${gradleVersion}"
}

Collection dirsByPattern(String baseDir, String pattern) {
  def paths = []
  fileTree(baseDir).visit { FileVisitDetails details ->
      if (details.isDirectory() && details.name ==~ pattern) paths << details.file.path
  }
  return paths
}

String dirName(File file) {
  file.getParentFile().getPath()
}

task installDependencies(type: Task) {
  doLast {
    dependencies.each { String dep ->
      exec {
        commandLine rootProject.python, '-m', 'pip', 'install', dep
      }
    }
  }
}

task cleanPython(type: Delete) {
  description = 'Clean all compiled and cached python files from "${sourceRoot}"'
  doLast {
    println "Cleaning up compiled and cached files ..."
    delete fileTree(sourceRoot).matching {
      include "**/*.pyc"
    }
    delete dirsByPattern(sourceRoot, /.*__pycache__$/)
  }
}

task compilePython(type: Task) {
  description = 'Compile all python files from "${sourceRoot}/main"'
  doLast {
    fileTree("${sourceRoot}/main").matching {
      include "**/*.py"
      exclude "**/__init__.py"
    }.each { File file ->
        println "Compiling Python file -> ${file.name}"
        exec {
          commandLine rootProject.python, '-m', 'py_compile', file.path
        }
      }
  }
}

task compilePythonTest(type: Task) {
  dependsOn compilePython
  description = 'Compile all python test files from "${sourceRoot}/test"'
  doLast {
    fileTree("${sourceRoot}/test").matching {
      include "**/test_*.py"
      exclude "**/__init__.py"
    }.each { File file ->
        println "Compiling Python Test file -> ${file.name}"
        exec {
          commandLine rootProject.python, '-m', 'py_compile', file.path
        }
      }
  }
}

task compileQrc(type: Task) {
  description = 'Compile all Qt resource files from "${sourceRoot}/main"'
  doLast {
    fileTree("${sourceRoot}/main").matching {
      include "**/*.qrc"
    }.each { File file ->
        println "Compiling Qt Resource -> ${file.name}"
        exec {
          commandLine rootProject.pyrcc, '-m', 'py_compile', file.path
        }
      }
  }
}

task compile(type: Task) {
  dependsOn installDependencies
  mustRunAfter installDependencies
  dependsOn compilePython
  dependsOn compileQrc
  dependsOn compilePythonTest
}

task runTest(type: Task) {
  dependsOn compile
  description = 'Run all Qt python tests "${sourceRoot}/test/**/test_*.py"'
  doLast {
    fileTree("${sourceRoot}/test").matching {
      include "**/test_*.py"
    }.each { File file ->
        println "Running unittests -> ${file.name}"
        exec {
          workingDir = dirName(file)
          environment PYTHONPATH: "${rootDir}:${sourceRoot}"
          commandLine rootProject.python, '-m', 'unittest', '-f', file.path
        }
      }
  }
}

task installModule(type: Task) {
  dependsOn compilePython
  description = 'Install all python modules from "**/setup.py"'
  doLast {
    fileTree("${sourceRoot}").matching {
      include "**/setup.py"
    }.each { File module ->
        def moduleDir = dirName(module)
        println "Installing ${rootProject.name} from ${moduleDir} -> ${module.path}"
        exec {
          workingDir "${moduleDir}"
          commandLine rootProject.python, '-m', 'pip', 'install', "."
        }
      }
  }
}

task uninstallModule(type: Task) {
  description = 'Uninstall all python modules from "**/setup.py"'
  doLast {
    fileTree("${sourceRoot}/main").matching {
      include "**/setup.py"
    }.each { File file ->
        def moduleDir = "${file.path.replace(file.name, "")}"
        println "Removing python modules from: ${file.path}"
        exec {
          workingDir "${moduleDir}"
          commandLine rootProject.python, '-m', 'pip', 'uninstall', '-y', "${rootProject.name}"
        }
      }
  }
}

task clean(type: Task) {
  dependsOn cleanPython
}

task test(type: Task) {
  dependsOn runTest
  dependsOn compile
  mustRunAfter runTest
}

task build(type: Task) {
  dependsOn clean
  mustRunAfter clean
  dependsOn test
}

task install(type: Task) {
  dependsOn build
  mustRunAfter build
  dependsOn uninstallModule
  mustRunAfter uninstallModule
  dependsOn installModule
}
